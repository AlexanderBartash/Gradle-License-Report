plugins {
    id 'com.gradle.build-scan' version '2.2.1'
    id 'idea'
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id "com.palantir.idea-test-fix" version "0.1.0"
    id "com.github.hierynomus.license" version"0.15.0"
}

buildScan {
    termsOfServiceUrl   = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

group = "com.github.jk1"
version = "1.8"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude group: 'org.codehaus.groovy'
    }
}

idea {
    module {
        downloadSources = true
        outputDir file("$buildDir/idea/main")
        testOutputDir file("$buildDir/idea/test")
    }
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false }
}

// local maven publishing to ease development
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.jk1.dependency-license-report'
            artifactId 'com.github.jk1.dependency-license-report.gradle.plugin'
            from components.java
        }
    }
}

// release publishing to gradle plugin repo
pluginBundle {
    website = 'https://github.com/jk1/Gradle-License-Report'
    vcsUrl = 'https://github.com/jk1/Gradle-License-Report'
    description = 'A plugin for generating reports about the licenses of the dependencies for your Gradle project'
    tags = ['dependency-management', 'license', 'reporting']
    plugins {
        licenseReportPlugin {
            id = 'com.github.jk1.dependency-license-report'
            displayName = 'Gradle dependency license report plugin'
        }
    }
}
