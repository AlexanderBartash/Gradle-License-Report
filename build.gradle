plugins{
    id 'idea'
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.9'
    id "com.palantir.idea-test-fix" version "0.1.0"
}

group = "com.github.jk1"
version = "0.6.1"

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude group: 'org.codehaus.groovy'
    }
}

task('sourcesJar', type: Jar, dependsOn: classes) {
    classifier = 'sources'
    sourceSets.each { SourceSet ss ->
        from ss.allSource
    }
}

task('javadocJar', type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

idea {
    module {
        downloadSources = true
        outputDir file("$buildDir/idea/main")
        testOutputDir file("$buildDir/idea/test")
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

// local maven publishing to ease development
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
        }
    }
}

// release publishing to gradle plugin repo
pluginBundle {
    website = 'https://github.com/jk1/Gradle-License-Report'
    vcsUrl = 'https://github.com/jk1/Gradle-License-Report'
    description = 'A plugin for generating reports about the licenses of the dependencies for your Gradle project'
    tags = ['dependency-management', 'license', 'reporting']
    plugins {
        licenseReportPlugin {
            id = 'com.github.jk1.dependency-license-report'
            displayName = 'Gradle dependency license report plugin'
        }
    }
}
